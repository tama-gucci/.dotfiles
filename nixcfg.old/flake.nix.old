{
  # Human-readable description of what this flake provides
  description = "NixOS System Configuration";

  # INPUTS: External dependencies (like package.json dependencies)
  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable";
    chaotic.url = "github:chaotic-cx/nyx/nyxpkgs-unstable";
    nixos-hardware.url = "github:NixOS/nixos-hardware/master";
    
    lanzaboote = {
      url = "github:nix-community/lanzaboote/v0.4.3";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    home-manager = {
      url = "github:nix-community/home-manager";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    noctalia = {
      url = "github:noctalia-dev/noctalia-shell";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    zen-browser = {
      url = "github:0xc000022070/zen-browser-flake";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  # OUTPUTS: What this flake produces (the actual system configuration)
  outputs = { nixpkgs, home-manager, chaotic, lanzaboote, ... }@inputs: 
  let
    # Helper function to create a NixOS system
    mkSystem = { hostname, system ? "x86_64-linux" }:
      nixpkgs.lib.nixosSystem {
        inherit system;
        specialArgs = { inherit inputs; };
        modules = [
          ./modules
          home-manager.nixosModules.home-manager
          chaotic.nixosModules.default
          lanzaboote.nixosModules.lanzaboote 
          ./hosts/${hostname}
        ];
      };
  in
  {
    nixosConfigurations = {
      obelisk = mkSystem { hostname = "obelisk"; };
    };
  };

  # Binary caches - download pre-built packages instead of compiling locally
  nixConfig = {
    extra-substituters = [
      "https://nix-community.cachix.org"  # Community packages
    ];
    extra-trusted-public-keys = [
      "nix-community.cachix.org-1:mB9FSh9qf2dCimDSUo8Zy7bkq5CX+/rkCWyvRCYg3Fs="
    ];
  };
}
